(define (domain UAV)

  (:requirements :typing :durative-actions :fluents :timed-initial-literals :time :conditional-effects :equality) 
  (:types spiral smallLawnmower largeLawnmower - pattern waypoint orientation)
  (:predicates (updated ?p ?o) (at ?p - waypoint) (active ?p - pattern) (beginAt ?w - waypoint  ?p - pattern) (endAt ?w - waypoint ?p - pattern) (searched ?p - pattern ?o - orientation) (oriented ?p - pattern ?o - orientation) (not-oriented ?p - pattern ?o - orientation))
  (:functions (reward) (rewardof ?p - pattern) (timefor ?p - pattern) (distance ?p1 ?p2 - waypoint) (penalty ?p - pattern) (award ?p - pattern))

(:event updateReward
:parameters (?p - pattern ?o - orientation)
:precondition (and (updated ?p ?o))
:effect (and (not (updated ?p ?o)) (forall (?p1 - pattern) (when (oriented ?p1 ?o) (and (decrease (rewardof ?p1) (penalty ?p1)))))
                    (forall (?p2 - pattern) (when (not-oriented ?p2 ?o) (and (increase (rewardof ?p2) (award ?p2))))))
)

(:durative-action fly
 :parameters (?from ?to - waypoint)
 :duration (= ?duration (distance ?from ?to))
 :condition (and (at start (at ?from)) (at start (not (= ?from ?to))))
 :effect (and (at start (not (at ?from))) (at end (at ?to)))
)
 
(:durative-action doSpiral
:parameters (?from ?to - waypoint ?p - spiral ?o - orientation)
:duration (=?duration (timefor ?p))
:condition (and (at start (beginAt ?from ?p)) (at start (endAt ?to ?p)) (at start (oriented ?p ?o)) 
			(at start (at ?from)) (at end (active ?p)))
:effect (and (at end (at ?to)) (at start (not (at ?from))) (at end (increase (reward) (rewardof ?p))) 
	(at end (searched ?p ?o)) (at end (updated ?p ?o)) (at start (not (oriented ?p ?o))))
)

(:durative-action doSmallLawnmower
:parameters (?from ?to - waypoint ?p - smallLawnmower ?o - orientation)
:duration (=?duration (timefor ?p))
:condition (and (at start (beginAt ?from ?p)) (at start (endAt ?to ?p)) (at start (oriented ?p ?o))  
			(at start (at ?from)) (at end (active ?p)))
:effect (and (at end (at ?to)) (at start (not (at ?from))) (at end (increase (reward) (rewardof ?p))) 
	(at end (searched ?p ?o))(at end (updated ?p ?o))(at start (not (oriented ?p ?o))))
)
)


(define (problem UAVprob1)
(:domain UAV)
(:objects spiral1 spiral2 - spiral
          smallLawn6 smallLawn7 - smallLawnmower
          origin s1s s1e s2s s2e sl6s sl6e sl7s sl7e  - waypoint
	  o1 o2 o3 - orientation)
(:init
  (= (reward) 0)
  (at origin)
  (= (rewardOf spiral1) 6061)
  (at 383 (= (rewardOf spiral1) 12122))
  (at 394 (= (rewardOf spiral1) 6061))
  (= (timefor spiral1) 299)
  (beginAt s1s spiral1)
  (endAt s1e spiral1)
  (at 329 (active spiral1))
  (at 411 (not (active spiral1)))
  (oriented spiral1 o1)
  (not-oriented spiral1 o2)
  (not-oriented spiral1 o3)
  (= (penalty spiral1) 2000) 
  (= (award spiral1) 2000)	
  (= (distance origin s1s)41)
  (= (distance s1e s2s) 132)
  (= (distance s1e sl6s) 289)
  (= (distance s1e sl7s) 294)
  (= (rewardOf spiral2) 106)
  (at 1979 (= (rewardOf spiral2) 213))
  (at 2166 (= (rewardOf spiral2) 106))
  (= (timefor spiral2) 299)
  (beginAt s2s spiral2)
  (endAt s2e spiral2)
  (at 1046 (active spiral2))
  (at 2446 (not (active spiral2)))
  (oriented spiral2 o1)
  (not-oriented spiral2 o2)
  (not-oriented spiral2 o3)
  (= (penalty spiral2) 100) 
  (= (award spiral2) 100)		
  (= (distance origin s2s)119)
  (= (distance s2e s1s) 107)
  (= (distance s2e sl6s) 235)
  (= (distance s2e sl7s) 257)
  (= (rewardOf smallLawn6) 151)
  (at 3163 (= (rewardOf smallLawn6) 302))
  (at 3360 (= (rewardOf smallLawn6) 151))
  (= (timefor smallLawn6) 440)
  (beginAt sl6s smallLawn6)
  (endAt sl6e smallLawn6)
  (at 2181 (active smallLawn6))
  (at 3655 (not (active smallLawn6)))
  (oriented smallLawn6 o1)
  (not-oriented smallLawn6 o2)
  (not-oriented smallLawn6 o3)
  (= (penalty smallLawn6) 100) 
  (= (award smallLawn6) 100)		
  (= (distance origin sl6s)335)
  (= (distance sl6e s1s) 216)
  (= (distance sl6e s2s) 185)
  (= (distance sl6e sl7s) 111)
  (= (rewardOf smallLawn7) 106)
  (at 1849 (= (rewardOf smallLawn7) 212))
  (at 1960 (= (rewardOf smallLawn7) 106))
  (= (timefor smallLawn7) 266)
  (beginAt sl7s smallLawn7)
  (endAt sl7e smallLawn7)
  (at 1296 (active smallLawn7))
  (at 2126 (not (active smallLawn7)))
  (oriented smallLawn7 o3)
  (not-oriented smallLawn7 o2)
  (not-oriented smallLawn7 o1)
  (= (penalty smallLawn7) 100) 
  (= (award smallLawn7) 100)	
  (= (distance origin sl7s)345)
  (= (distance sl7e s1s) 247)
  (= (distance sl7e s2s) 239)
  (= (distance sl7e sl6s) 57)
)
(:goal (and (>= (reward) 1)))
(:metric maximize (reward)))
